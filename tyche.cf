-- layouts

layout toplevel ;
layout "do", "else", "of" ;

-- programs

entrypoints Program ;

Program.   Program ::= "{" [Stmt] "}" ;

-- function arguments

Arg.       Arg     ::= ArgMod Ident ":" FullType ;
separator  Arg "," ;

-- statements

Skip.        Stmt ::= "skip" ;
Break.       Stmt ::= "break" ;
Continue.    Stmt ::= "continue" ;
Ret.         Stmt ::= "return" Expr ;
VarDef.      Stmt ::= "def" Ident ":" FullType "=" Expr1 ;
Ass.         Stmt ::= Ident "=" Expr1 ;

FnDef.       Stmt ::= "def" Ident ":" FullType "(" [Arg] ")" "do" "{" [Stmt] "}" ;

Cond.        Stmt ::= "if" Expr "do" "{" [Stmt] "}" ;
CondElse.    Stmt ::= "if" Expr "do" "{" [Stmt] "}" "else" "{" [Stmt] "}" ;
While.       Stmt ::= "while" Expr "do" "{" [Stmt] "}" ;
ForList.     Stmt ::= "for" "each" Ident "from" Expr "do" "{" [Stmt] "}" ;
ForRange.    Stmt ::= "for" Ident "from" Expr "to" Expr "do" "{" [Stmt] "}" ;

separator Stmt ";" ;

-- types

Int.      Type     ::= "int" ;
Str.      Type     ::= "string" ;
Bool.     Type     ::= "boolean" ;
Void.     Type     ::= "void" ;
Float.    Type     ::= "float" ;
List.     Type     ::= "[" FullType "]" ;
Array.    Type     ::= "{" FullType "}" ;
Fun.      Type     ::= "(" [ArgType] ")" "->" FullType ;
ArgType.  ArgType  ::= ArgMod FullType ;
FullType. FullType ::= [TypeMod] Type ;
separator ArgType "," ;

-- argument modifiers

AModVar.   ArgMod ::= "var" ;
AModVal.   ArgMod ::= "val" ;
AModInOut. ArgMod ::= "inout" ;

-- type modifiers

TModReadonly. TypeMod ::= "readonly" ;
separator TypeMod "" ;

-- expressions

ELitVoid.  Expr9 ::= "void" ;
EVar.      Expr9 ::= Ident ;
ELitInt.   Expr9 ::= Integer ;
ELitTrue.  Expr9 ::= "true" ;
ELitFalse. Expr9 ::= "false" ;
EString.   Expr9 ::= String ;
ELitFloat. Expr9 ::= Double ;
EEmpList.  Expr9 ::= "[]" ":" FullType ;
EApp.      Expr9 ::= Expr9 "(" [Expr] ")" ;

Neg.       Expr8 ::= "-" Expr9 ;
Not.       Expr8 ::= "!" Expr9 ;
Not.       Expr8 ::= "not" Expr9 ;

ECons.     Expr7 ::= Expr8 "::" Expr7 ;

EMul.      Expr6 ::= Expr6 MulOp Expr7 ;

EAdd.      Expr5 ::= Expr5 AddOp Expr6 ;

ERel.      Expr4 ::= Expr4 RelOp Expr5 ;

EAnd.      Expr3 ::= Expr4 AndOp Expr3 ;

EOr.       Expr2 ::= Expr3 OrOp Expr2 ;

EList.     Expr1 ::= "[" [Expr] "]" ;
EArr.      Expr1 ::= "{" [Expr] "}" ;
EArrSize.  Expr1 ::= "array" ":" FullType "[" Expr "]" ;
EArrApp.   Expr1 ::= Expr2 "[" Expr "]" ;

EIf.       Expr ::= Expr1 "?" Expr1 ":" Expr1 ;
ELambda.   Expr ::= "lambda" ":" FullType "(" [Arg] ")" "->" [Stmt] ;
ERand.     Expr ::= "random" "from" Expr1 ;
ERandDist. Expr ::= "random" "from" Expr1 "distribution" Expr1 ;
EProbSamp. Expr ::= "probability" "tested" Expr1 "times" "of" "{" [Stmt] "}" "satisfying" Expr1 ;

coercions Expr 9 ;
separator Expr "," ;

-- operators

Plus.   AddOp ::= "+" ;
Minus.  AddOp ::= "-" ;
Times.  MulOp ::= "*" ;
Div.    MulOp ::= "/" ;
Mod.    MulOp ::= "%" ;
Mod.    MulOp ::= "mod" ;
LTH.    RelOp ::= "<" ;
LE.     RelOp ::= "<=" ;
GTH.    RelOp ::= ">" ;
GE.     RelOp ::= ">=" ;
EQU.    RelOp ::= "==" ;
EQU.    RelOp ::= "equals" ;
NE.     RelOp ::= "!=" ;
Or.     OrOp  ::= "or" ;
Or.     OrOp  ::= "||" ;
And.    AndOp ::= "and" ;
And.    AndOp ::= "&&" ;

-- comments
comment "#" ;
comment "//" ;
comment "--" ;
comment "/*" "*/" ;
