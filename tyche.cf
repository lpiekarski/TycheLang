-- programs

entrypoints Program ;

Program.   Program ::= Stmt ;

Arg.       Arg     ::= ArgMod FullIdent ":" FullType ;
separator  Arg "," ;

-- identifiers

FullIdent. FullIdent ::= Ident ;
AnonIdent. FullIdent ::= "_" ;

-- statements

Skip.        Stmt5 ::= ";" ;
Break.       Stmt5 ::= "break" ";" ;
Continue.    Stmt5 ::= "continue" ";" ;
Ret.         Stmt5 ::= "return" Expr ";" ;
Ret.         Stmt5 ::= Expr ";" ;
VRet.        Stmt5 ::= "return" ";" ;
VarDef.      Stmt5 ::= "def" FullIdent ":" FullType "=" Expr1 ";" ;
Ass.         Stmt5 ::= Ident "=" Expr1 ";" ;

FnDef.       Stmt4 ::= "def" FullIdent ":" FullType "(" [Arg] ")" Stmt5 ;

Cond.        Stmt1 ::= "if" Expr "then" Stmt1 ;
CondElse.    Stmt1 ::= "if" Expr "then" Stmt1 "else" Stmt1 ;
While.       Stmt1 ::= "while" Expr "do" Stmt1 ;
ForList.     Stmt1 ::= "for" "each" Ident "from" Expr "do" Stmt1 ;
ForRange.    Stmt1 ::= "for" Ident "from" Expr "to" Expr "do" Stmt1 ;

--Composition. Stmt  ::= Stmt1 ";" Stmt ;
Composition. Stmt  ::= Stmt1 Stmt ;

_. Stmt  ::= Stmt1 ;
_. Stmt1 ::= Stmt2 ;
_. Stmt2 ::= Stmt3 ;
_. Stmt3 ::= Stmt4 ;
_. Stmt4 ::= Stmt5 ;
_. Stmt5 ::= Stmt6 ;
_. Stmt6 ::= "{" Stmt "}" ;

-- types

Int.      Type     ::= "int" ;
Str.      Type     ::= "string" ;
Bool.     Type     ::= "boolean" ;
Void.     Type     ::= "void" ;
Float.    Type     ::= "float" ;
List.     Type     ::= "(" FullType ")" ;
Array.    Type     ::= "[" FullType "]" ;
Fun.      Type     ::= "(" [ArgType] ")" "->" FullType ;
ArgType.  ArgType  ::= ArgMod FullType ;
FullType. FullType ::= [TypeMod] Type ;
separator ArgType "," ;

-- argument modifiers

AModVar.   ArgMod ::= "var" ;
AModVal.   ArgMod ::= "val" ;
AModInOut. ArgMod ::= "inout" ;

-- type modifiers

TModReadonly. TypeMod ::= "readonly" ;
separator TypeMod "" ;

-- expressions

EVar.      Expr9 ::= Ident ;
ELitInt.   Expr9 ::= Integer ;
ELitTrue.  Expr9 ::= "true" ;
ELitFalse. Expr9 ::= "false" ;
EString.   Expr9 ::= String ;
ELitFloat. Expr9 ::= Double ;
EEmpList.  Expr9 ::= "()" ":" FullType ;

Neg.       Expr8 ::= "-" Expr9 ;
Not.       Expr8 ::= "!" Expr9 ;
Not.       Expr8 ::= "not" Expr9 ;

ECons.     Expr7 ::= Expr8 "::" Expr7 ;

EMul.      Expr6 ::= Expr6 MulOp Expr7 ;

EAdd.      Expr5 ::= Expr5 AddOp Expr6 ;

ERel.      Expr4 ::= Expr4 RelOp Expr5 ;

EAnd.      Expr3 ::= Expr4 AndOp Expr3 ;

EOr.       Expr2 ::= Expr3 OrOp Expr2 ;

EList.     Expr1 ::= "(" [Expr] ")" ;
EArr.      Expr1 ::= "[" [Expr] "]" ;
EArrSize.  Expr1 ::= "array" ":" FullType "[" Expr "]" ;
EApp.      Expr1 ::= Expr2 "(" [Expr] ")" ;
EArrApp.   Expr1 ::= Expr2 "[" Expr "]" ;

EIf.       Expr ::= Expr1 "?" Expr1 ":" Expr1 ;
ELambda.   Expr ::= "lambda" ":" FullType "(" [Arg] ")" "->" Stmt1 ;
ERand.     Expr ::= "random" "from" Expr1 ;
ERandDist. Expr ::= "random" "from" Expr1 "distribution" Expr1 ;
EProb.     Expr ::= "probability" "of" Stmt1 "satisfying" Expr1 ;
EProbSamp. Expr ::= "probability" "tested" Expr1 "times" "of" Stmt1 "satisfying" Expr1 ;

coercions Expr 9 ;
separator Expr "," ;

-- operators

Plus.   AddOp ::= "+" ;
Minus.  AddOp ::= "-" ;
Times.  MulOp ::= "*" ;
Div.    MulOp ::= "/" ;
Mod.    MulOp ::= "%" ;
Mod.    MulOp ::= "mod" ;
LTH.    RelOp ::= "<" ;
LE.     RelOp ::= "<=" ;
GTH.    RelOp ::= ">" ;
GE.     RelOp ::= ">=" ;
EQU.    RelOp ::= "==" ;
EQU.    RelOp ::= "equals" ;
NE.     RelOp ::= "!=" ;
Or.     OrOp  ::= "or" ;
Or.     OrOp  ::= "||" ;
And.    AndOp ::= "and" ;
And.    AndOp ::= "&&" ;

-- comments
comment "#" ;
comment "//" ;
comment "--" ;
comment "/*" "*/" ;
